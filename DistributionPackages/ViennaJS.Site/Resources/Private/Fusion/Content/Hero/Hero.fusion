prototype(ViennaJS.Site:Content.Hero) < prototype(Neos.Neos:ContentComponent) {
	imageSource = Neos.Fusion:Case {
		image {
			condition = ${q(node).property('image') ? true : false}
			renderer = Sitegeist.Kaleidoscope:AssetImageSource {
				asset = ${q(node).property('image')}
				width = 1900
			}
		}
		dummyImage {
			condition = ${site.context.inBackend}
			renderer = Sitegeist.Kaleidoscope:DummyImageSource
		}
	}
	alternativeText = ${q(node).property('alternativeText')}

	renderer = afx`
		<Sitegeist.Kaleidoscope:Image
			@if.hasSrc={props.imageSource}
			class="viennajs-hero hero"
			alt={props.alternativeText}
			imageSource={props.imageSource}
			srcset="320w, 400w, 600w, 800w, 1000w, 1200w, 1600w"
			sizes="(min-width: 800px) 1000px, (min-width: 480px) 800px, (min-width: 320px) 440px, 100vw"
			/>
	`
}
